row1 = $fe
row2 = $ff

InterlockingVerticalHGR
         lda   #$00
         sta   row1
         lda   #$5F
         sta   row2
@loop    lda   row1
         asl
         jsr   HGRCalc
         ldy   #$27
-        lda   ($3c),y
         sta   ($26),y
         dey
         bpl   -
         lda   row2
         asl
         clc
         adc   #$01
         jsr   HGRCalc
         ldy   #$27
-        lda   ($3c),y
         sta   ($26),y
         dey
         bpl   -
         lda   #$30
         jsr   Wait
         lda   $C000
         bmi   @exit
         inc   row1
         dec   row2
         bpl   @loop
@exit    rts

HGRCalc
         asl
         tax
         and   #$F0
         bpl   @calc1
         ora   #$05
@calc1   bcc   @calc2
         ora   #$0A
@calc2   asl
         asl
         sta   $26
         txa
         and   #$0E
         adc   #$10
         asl   $26
         rol
         sta   $27
         eor   #$60
         sta   $3d
         lda   $26
         sta   $3c
         rts

Wait
         sec
@wait1   pha
@wait2   sbc   #1
         bne   @wait2
         pla
         bit   $C000
         bmi   @exitwait
         sbc   #1
         bne   @wait1
@exitwait
         rts
