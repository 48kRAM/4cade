;license:MIT
;(c) 2019 by 4am
;
; sound effects
;
; Public functions
; - SoftBell
;

;------------------------------------------------------------------------------
; SoftBell
;
; in:    none
; out:   all registers and flags preserved
;------------------------------------------------------------------------------
SoftBell
         php
         pha
         txa
         pha
         ldx   #32
-        lda   #2
         jsr   @wait
         bit   $C030
         lda   #33
         jsr   @wait
         bit   $C030
         dex
         bne   -
         pla
         tax
         pla
         plp
         rts
@wait ; identical to $FCA8 ROM routine, but ROM is switched out when we need it
         sec
--       pha
-        sbc   #1
         bne   -
         pla
         sbc   #1
         bne   --
         rts
