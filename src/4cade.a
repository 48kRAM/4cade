!cpu 6502
!to "build/LAUNCHER.SYSTEM",plain
*=$2000

         !source "src/constants.a"
         !source "src/macros.a"

         jsr   Has64K                ; check for 64K (required)
         bcs   @no64K
         jsr   DisableAccelerator    ; set to 1 MHz
         jsr   Has128K               ; check for 128K (absence is OK, we just filter out some games)
         ror   MachineStatus
         jsr   HasJoystick           ; check for joystick (absence is OK, we just filter out some games)
         ror   MachineStatus         ; now bit 6 = 1 if 128K
                                     ;     bit 7 = 1 if joystick

         jsr   init                  ; initialize ProRWTS2 (bye bye ProDOS)
         +READ_RAM1_WRITE_RAM1
         ldx   #$00                  ; relocate rest of program to RAM bank 1 in language card
@FM      lda   FirstMover,x
         sta   $D000,x
         inx
         bne   @FM
         lda   @FM+2
         cmp   #>LastMover
         bcs   +
         inc   @FM+2
         inc   @FM+5
         bne   @FM
+
         jmp   Start

@no64K
         jsr   $FB2F
         jsr   $FC58
         ldy   #@no64Klen
-        lda   @s_no64K,y
         sta   $6B6,y
         dey
         bpl   -
@hang    bmi   @hang
@s_no64K !raw  "REQUIRES 64K"
@no64Klen=*-@s_no64K

         ; ProRWTS2 has its own function to relocate itself
         !source "src/prorwts2.a"
         ; these routines will only be called once, from main memory, before relocating to language card
ProRWTSBuffer
         !source "src/hw.memcheck.a"
         !source "src/hw.joystick.a"
         !source "src/hw.normfast.a"
*=ProRWTSBuffer+512                  ; ProRWTS needs a 512-byte buffer for its init function
                                     ; so we reuse as much of the 1-time code as possible
                                     ; and fill the rest with zeros

FirstMover
!pseudopc $D000 {
!zone
Reenter                              ; self-running demos should call this to exit back to 4cade
         ; TODO
Start
         jsr   LoadFile              ; load games list from file
         !word kGameListConfFile

         jsr   ParseGamesList        ; parse games list
         !word gGamesListStore
         !word ldrlo2                ; (ldrlo2) points to load address

         +LDAY SRC
         +STAY gAttractModeStore     ; save pointer to free space for next store

         jsr   LoadFile              ; load attract-mode configuration from file
         !word kAttractModeConfFile

         jsr   ParseKeyValueList     ; parse attract-mode configuration
         !word gAttractModeStore
         !word ldrlo2                ; (ldrlo2) points to load address

         +LDAY SRC
         +STAY gSlideshowStore       ; save pointer to free space for next store

         jsr   AttractMode

         jmp   $FAA6

AttractMode
         jsr   okvs_nth              ; get filename of next attract module
         !word gAttractModeStore
         !byte 0
         +STAY PTR
         jsr   ResetPath
         +LDADDR kAttractModeSlideshowDirectory
         jsr   AddToPath
         +LDADDR kPathSeparator
         jsr   AddToPath
         +LDAY PTR
         jsr   AddToPath
         jsr   LoadFile              ; load attract module configuration file
         !word gPathname

         jsr   ParseKeyValueList     ; parse slideshow configuration
         !word gSlideshowStore
         !word ldrlo2                ; (ldrlo2) points to load address

         jsr   okvs_iter
         !word gSlideshowStore
         !word HGRLoad
         rts

HGRLoad
         ldx   $C000
         bmi   @exit
         +STAY PTR
         jsr   ResetPath
         +LDADDR kHGRScreenshotDirectory
         jsr   AddToPath
         +LDADDR kPathSeparator
         jsr   AddToPath
         +LDAY PTR
         jsr   AddToPath
         jsr   LoadFile
         !word gPathname
         jsr   FizzleHGR
@exit    rts

kGameListConfFile
         !byte @kGameListConfFile_e-@kGameListConfFile_b
@kGameListConfFile_b
         !text "GAMES.CONF"
@kGameListConfFile_e
kAttractModeConfFile
         !byte @kAttractModeConfFile_e-@kAttractModeConfFile_b
@kAttractModeConfFile_b
         !text "ATTRACT.CONF"
@kAttractModeConfFile_e

         ; these routines will only be called after relocating to language card
         !source "src/prodos.path.a"
         !source "src/glue.prorwts2.a"
         !source "src/okvs.a"
         !source "src/parse.common.a"
         !source "src/parse.games.a"
         !source "src/fx.hgr.fizzle.a"
         !source "src/fx.dhgr.fizzle.a"
gGamesListStore
         !word *+2                   ; address of first okvs store
}
LastMover
