!cpu 6502
!to "build/LAUNCHER.SYSTEM",plain
*=$2000

         !source "src/constants.a"
         !source "src/macros.a"

         jsr   Has64K                ; check for 64K (required)
         bcs   @no64K
         jsr   DisableAccelerator    ; set to 1 MHz
         jsr   Has128K               ; check for 128K (absence is OK, we just filter out some games)
         ror   MachineStatus
         jsr   HasJoystick           ; check for joystick (absence is OK, we just filter out some games)
         ror   MachineStatus         ; now bit 6 = 1 if 128K
                                     ;     bit 7 = 1 if joystick

         +READ_ROM_WRITE_RAM1
         ldx   #$00                  ; relocate rest of program to RAM bank 1 in language card
@FM      lda   FirstMover,x
         sta   $D000,x
         inx
         bne   @FM
         lda   @FM+2
         cmp   #>LastMover
         bcs   +
         inc   @FM+2
         inc   @FM+5
         bne   @FM
+
         jsr   init                  ; initialize ProRWTS2 (bye bye ProDOS)
         +READ_RAM1_WRITE_RAM1
         jmp   Start

@no64K
         jsr   $FB2F
         jsr   $FC58
         ldy   #@no64Klen
-        lda   @s_no64K,y
         sta   $6B6,y
         dey
         bpl   -
@hang    bmi   @hang
@s_no64K !raw  "REQUIRES 64K"
@no64Klen=*-@s_no64K

         ; these routines will only be called once, from main memory, before relocating to language card
         !source "src/hw.memcheck.a"
         !source "src/hw.joystick.a"
         !source "src/hw.normfast.a"
         ; ProRWTS2 has its own function to relocate itself
         !source "src/prorwts2.a"

FirstMover
!pseudopc $D000 {
!zone
Reenter                              ; self-running demos should call this to exit back to 4cade
         ; TODO
Start
         jsr   LoadFile              ; load games list from file
         !word kGameListConfFile

         jsr   ParseGamesList        ; parse games list
         !word gGamesListStore
         !word @fileBuffer

         +LDAY SRC
         +STAY gAttractModeStore     ; save pointer to free space for next store

         jsr   LoadFile              ; load attract-mode configuration from file
         !word kAttractModeConfFile

         jsr   ParseKeyValueList     ; parse attract-mode configuration
         !word gAttractModeStore
         !word @fileBuffer

         lda   #$00
-        beq   -

@fileBuffer
         !word $4000

         jsr   okvs_iter_values
         !word gGamesListStore
         !word HGRLoad
         brk

HGRLoad
         ldx   $C000
         bmi   @exit
         +STAY PTR
         jsr   ResetPath
         +LDADDR kHGRScreenshotDirectory
         jsr   AddToPath
         +LDADDR kPathSeparator
         jsr   AddToPath
         +LDAY PTR
         jsr   AddToPath
         jsr   LoadFile
         !word gPathname
         jsr   FizzleHGR
@exit    rts

kGameListConfFile
         !byte @kGameListConfFile_e-@kGameListConfFile_b
@kGameListConfFile_b
         !text "GAMES.CONF"
@kGameListConfFile_e
kAttractModeConfFile
         !byte @kAttractModeConfFile_e-@kAttractModeConfFile_b
@kAttractModeConfFile_b
         !text "ATTRACT.CONF"
@kAttractModeConfFile_e

         ; these routines will only be called after relocating to language card
         !source "src/prodos.path.a"
         !source "src/glue.prorwts2.a"
         !source "src/okvs.a"
         !source "src/parse.common.a"
         !source "src/parse.games.a"
         !source "src/fx.hgr.fizzle.a"
         !source "src/fx.dhgr.fizzle.a"
gGamesListStore
         !word *+2                   ; address of first okvs store
}
LastMover
