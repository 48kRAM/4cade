;license:MIT
;(c) 2019 by 4am
;
!cpu 6502
!to "build/FX/ARROW.WHITE",plain
*=$6000

y = $fc
row = $fd
col = $fe
counter = $ff

         !source "src/fx/macros.a"

         +INIT_MASKS sourcemasks1, copymasks1
         +INIT_MASKS sourcemasks4, copymasks4

         lda   #(40+12+1)
         sta   counter

         lda   #39
         sta   col
ColLoop
         lda   #11
         sta   row
         ldy   col
         sty   y
RowLoop
         ; check if this column is visible
         ldy   y
         +IS_Y_OFFSCREEN
         +LBCS Skip1

         ; do top half of arrow (part 1 of 2)
         lda   row
         asl
         asl
         asl
         +HGR_CALC

         ldx   #7
-        lda   ($26),y
         and   sourcemasks1,x
         sta   $00
         lda   #$FF
         and   copymasks1,x
         ora   $00
         sta   ($26),y
         clc
         +HGR_INC_WITHIN_BLOCK
         dex
         bpl   -

         ; do bottom half of arrow (opposing row, same col)
         lda   #23
         sec
         sbc   row
         asl
         asl
         asl
         +HGR_CALC

         ldx   #7
-        lda   ($26),y
         and   sourcemasks4,x
         sta   $00
         lda   #$FF
         and   copymasks4,x
         ora   $00
         sta   ($26),y
         clc
         +HGR_INC_WITHIN_BLOCK
         dex
         bpl   -

Skip1
         ; now check if *this* column is visible
         iny
         sty   y
         +IS_Y_OFFSCREEN
         +LBCS Skip2

         ; do top half of arrow (part 2 of 2)
         lda   row
         asl
         asl
         asl
         +HGR_CALC

         ldx   #7
-        lda   #$FF
         and   sourcemasks1,x
         sta   $00
         lda   ($3c),y
         and   copymasks1,x
         ora   $00
         sta   ($26),y
         clc
         +HGR_INC_WITHIN_BLOCK
         dex
         bpl   -

         ; do bottom half of arrow (opposing row, same col)
         lda   #23
         sec
         sbc   row
         asl
         asl
         asl
         +HGR_CALC

         ldx   #7
-        lda   #$FF
         and   sourcemasks4,x
         sta   $00
         lda   ($3c),y
         and   copymasks4,x
         ora   $00
         sta   ($26),y
         clc
         +HGR_INC_WITHIN_BLOCK
         dex
         bpl   -

Skip2
         ; now check if *this* column is visible
         iny
         +IS_Y_OFFSCREEN
         bcs   NextRow

         ; do top half copy
         lda   row
         jsr   HGRBlockCopy

         ; do bottom half copy
         lda   #23
         sec
         sbc   row
         jsr   HGRBlockCopy

NextRow
         dec   row
         +LBPL RowLoop
         lda   $c000
         bmi   @exit
         dec   col
         dec   counter
         +LBNE ColLoop
@exit    rts

sourcemasks1
         !byte 0,0,0,0,0,0,0,0       ; SMC
sourcemasks4
         !byte 0,0,0,0,0,0,0,0       ; SMC
copymasks1
         !byte %11111111
         !byte %11111110
         !byte %11111100
         !byte %11111000
         !byte %11110000
         !byte %11100000
         !byte %11000000
         !byte %10000000
copymasks4
         !byte %10000000
         !byte %11000000
         !byte %11100000
         !byte %11110000
         !byte %11111000
         !byte %11111100
         !byte %11111110
         !byte %11111111

         !source "src/wait.a"
         !source "src/fx/fx.hgr.common.a"
