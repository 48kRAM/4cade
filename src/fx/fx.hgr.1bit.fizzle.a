;license:MIT
;(c) 2017-2020 by qkumba/4am

!cpu 6502
!to "build/FX/BIT.FIZZLE",plain
*=$6000

copymasks = $6100         ; [256 bytes]
addrs     = $6200         ; [256 bytes]

         jsr   swapzp
         ldx   #0
         lda   #1
--       ldy   #$20
-        sta   copymasks, x
         inx
         dey
         bne   -
         asl
         bne   --
         clc
--       ldy   #$20
         tya
-        sta   addrs, x
         adc   #$1
         inx
         dey
         bne   -
         txa
         bne   --
         sty   <rnd2+1
         iny
         sty   <rnd1+1
         jsr   $0
swapzp   ldx   #(end-start-1)
-        lda   $0, x
         pha
         lda   start, x
         sta   $0, x
         pla
         sta   start, x
         dex
         bpl   -
         rts

start
!pseudopc 0 {
loop     lda   <rnd1+1
loop2    eor   #$ff
         tay
         lsr   <rnd2+1
         ror   <rnd1+1
         bcc   +
         lda   <rnd2+1
         eor   #$B4
         sta   <rnd2+1
+        lda   addrs, x
         sta   <dst+2
         eor   #$60
         sta   <src+2
         lda   (<dst+1), y
src      eor   $FD00, y
         and   copymasks, x
         eor   (<dst+1), y
dst      sta   $FD00, y
rnd2     ldx   #$FD
         bne   loop
         lda   $c000
         bmi   exit
rnd1     lda   #$FD
         cmp   #1
         bne   loop2
exit     rts
}
end
