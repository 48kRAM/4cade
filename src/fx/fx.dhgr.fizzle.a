;license:MIT
;(c) 2017-2020 by qkumba/4am/John Brooks
;
!cpu 6502
!to "build/FX/DHGR.FIZZLE",plain
*=$6000

         ldx   #(end-start)          ; copy LFSR code to zero page
-        lda   start-1, x
         sta   $0, x
         dex
         bne   -
         txa
         tay
         pha
         pha
         rts

start
!pseudopc 1 {
; in: X,Y=0
loop     txa
loop1    eor   #$35                  ; LFSR form 0x3500 with period 16383
         tax
loop2    lda   addrs, x
         bpl   +
         and   #$7F
         sta   $C003
         sta   $C005
+        sta   <dst+2
         eor   #$60
         sta   <src+2
src      lda   $FD00, y
dst      sta   $FD00, y
         sta   $C002
         sta   $C004
         txa
         lsr
         tax
         tya
         ror
         tay
         bcc   loop2
         bne   loop
         bit   $C000
         bmi   exit
         txa
         bne   loop1
exit     lda   $4000                 ; last lousy byte (because LFSR never hits 0)
         sta   $2000
         rts
}
end
addrs
         !byte $20,$21,$22,$23,$24,$25,$26,$27,$28,$29,$2A,$2B,$2C,$2D,$2E,$2F
         !byte $30,$31,$32,$33,$34,$35,$36,$37,$38,$39,$3A,$3B,$3C,$3D,$3E,$3F
         !byte $A0,$A1,$A2,$A3,$A4,$A5,$A6,$A7,$A8,$A9,$AA,$AB,$AC,$AD,$AE,$AF
         !byte $B0,$B1,$B2,$B3,$B4,$B5,$B6,$B7,$B8,$B9,$BA,$BB,$BC,$BD,$BE,$BF
