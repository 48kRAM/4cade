;license:MIT
;(c) 2019 by 4am
;
!cpu 6502
!to "build/FX/DIAGONAL4",plain
*=$6000

counter = $fc
row = $fd
col = $fe
tmp = $ff

         ldx   #7
-        lda   copymasks,x
         eor   #%11111111
         sta   sourcemasks,x
         dex
         bpl   -
         lda   #(40+24)
         sta   counter

         lda   #39
         sta   col
@colloop
         lda   #0
         sta   row
         ldy   col
@rowloop
         tya
         bmi   +
         cpy   #40
         bcs   +
         lda   row
         jsr   HGRDiagonalBlockCopyWithGuards
+        iny
         bmi   +
         cpy   #40
         bcs   +
         lda   row
         jsr   HGRBlockCopy
+
         inc   row
         lda   row
         cmp   #24
         bcc   @rowloop
         lda   #64
         jsr   WaitForKeyWithTimeout
         bmi   @exit
         dec   col
         dec   counter
         bne   @colloop
@exit    rts

HGRDiagonalBlockCopyWithGuards
; in:    A = HGR row / 8 (0x00..0x17)
;        Y = HGR column (0x00..0x27)
; out:   Y preserved
;        A/X clobbered
;        tmp clobbered
         asl
         asl
         asl
         jsr   HGRCalc
         clc
         tya
         bmi   @exit
         ldx   #7
@loop
         lda   ($26),y
         and   sourcemasks,x
         sta   tmp
         lda   ($3c),y
         and   copymasks,x
         ora   tmp
         sta   ($26),y
         lda   $27
         adc   #$04
         sta   $27
         eor   #$60
         sta   $3d
         dex
         bpl   @loop
@exit    rts
sourcemasks
         !byte 0,0,0,0,0,0,0,0       ; SMC, will be copymasks EOR #$FF
copymasks
         !byte %10000000
         !byte %11000000
         !byte %11100000
         !byte %11110000
         !byte %11111000
         !byte %11111100
         !byte %11111110
         !byte %11111111

         !source "src/wait.a"
         !source "src/fx/fx.hgr.common.a"
